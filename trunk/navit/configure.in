AC_INIT(navit, 0.2.0)
AC_CONFIG_MACRO_DIR([m4])
SOURCE_MODE=svn

AM_INIT_AUTOMAKE
AM_CONFIG_HEADER(config.h)

AC_SUBST(SOURCE_MODE)
AM_CONDITIONAL(SOURCE_MODE_SVN, [test "x${SOURCE_MODE}" = "xsvn"])

if test "x${SOURCE_MODE}" = "xsvn" ; then
	USE_MAINTAINER_MODE=yes
else
	USE_MAINTAINER_MODE=no
fi

AC_DEFUN([AM_MAINTAINER_MODE],
[AC_MSG_CHECKING([whether to enable maintainer-specific portions of Makefiles])
  dnl maintainer-mode is enabled by default (reason of inclusion of this function)
  AC_ARG_ENABLE(maintainer-mode,
[  --enable-maintainer-mode  enable make rules and dependencies not useful
                          (and sometimes confusing) to the casual installer],
      USE_MAINTAINER_MODE=$enableval)
  AC_MSG_RESULT([$USE_MAINTAINER_MODE])
  AM_CONDITIONAL(MAINTAINER_MODE, [test $USE_MAINTAINER_MODE = yes])
  MAINT=$MAINTAINER_MODE_TRUE
  AC_SUBST(MAINT)dnl
]
)

AU_DEFUN([jm_MAINTAINER_MODE], [AM_MAINTAINER_MODE])

AM_MAINTAINER_MODE


plugins=yes; plugins_reason=default
postgresql=yes; postgresql_reason=default
samplemap=yes; samplemap_reason=default
binding_dbus=yes; binding_dbus_reason=default
binding_python=yes; binding_python_reason=default
font_freetype=yes; font_freetype_reason=default
fontconfig=yes; fontconfig_reason=default
gui_gtk=no; gui_gtk_reason=default
gui_win32=no; gui_win32_reason=default
gui_internal=yes; gui_internal_reason=default
gui_qml=no; gui_qml_reason=default
graphics=yes; graphics_reason=default
graphics_gd=no; graphics_gd_reason=default
graphics_gtk_drawing_area=no; graphics_gtk_drawing_area_reason=default
graphics_qt_qpainter=yes; graphics_qt_qpainter_reason=default
graphics_opengl=yes; graphics_opengl_reason=default
graphics_sdl=yes; graphics_sdl_reason=default
graphics_win32=no; graphics_win32_reason=default
plugin_pedestrian=no; plugin_pedestrian_reason=default
routing=yes; routing_reason=default
speech_android=no; speech_android_reason=default
speech_cmdline=yes; speech_cmdline_reason=default
speech_dbus=no; speech_dbus_reason=default
speech_espeak=no; speech_espeak_reason=default
speech_speech_dispatcher=yes; speech_speech_dispatcher_reason=default
vehicle_demo=yes; vehicle_demo_reason=default
vehicle_file=yes; vehicle_file_reason=default
vehicle_gpsd=yes; vehicle_gpsd_reason=default
vehicle_gpsd_dbus=no; vehicle_gpsd_dbus_reason=default
vehicle_gypsy=yes; vehicle_gypsy_reason=default
vehicle_null=no; vehicle_null_reason=default
vehicle_wince=no; vehicle_wince_reason=default
vehicle_iphone=no; vehicle_iphone_reason=default
vehicle_android=no; vehicle_android_reason=default
graphics_android=no; graphics_android_reason=default
vehicle_maemo=no; vehicle_maemo_reason=default

shared_libnavit=no
LIBNAVIT=navit
bin_navit=yes

AC_CANONICAL_HOST
win32=no
win32ce=no
case $host_os in
wince|mingw32ce|cegcc)
	win32=yes
	win32ce=yes
	AC_DEFINE(HAVE_API_WIN32_BASE, 1, [Have Windows Base API])
	AC_DEFINE(HAVE_API_WIN32_CE, 1, [Have Windows CE API])
	gui_win32=yes; gui_win32_reason="host_os is wince"
	graphics_win32=yes; graphics_win32_reason="host_os is wince"
	vehicle_wince=yes; vehcile_wince_reason="host_os is wince"
	speech_espeak=yes; speech_espeak_reason="host_os is wince"
	support_libpng=yes
	;;
mingw32)
	win32=yes
	AC_DEFINE(HAVE_API_WIN32_BASE, 1, [Have Windows Base API])
	AC_DEFINE(HAVE_API_WIN32, 1, [Have Windows API])
	gui_win32=yes; gui_win32_reason="host_os is mingw32"
	graphics_win32=yes; graphics_win32_reason="host_os is mingw32"
	speech_espeak=yes; speech_espeak_reason="host_os is mingw32"
	support_libpng=yes
	;;
linux*_android)
	android=yes
	shared_libnavit=yes
	bin_navit=no
	AC_DEFINE(HAVE_API_ANDROID, 1, [Have Android API])
	echo "void dl_unwind_find_exidx(void) {}" >crt0.c
	$CC -c crt0.c
	$AR r libg.a crt0.o
	vehicle_android=yes; vehicle_android_reason="host_os is android"
	graphics_android=yes; graphics_android_reason="host_os is android"
	speech_android=yes; speech_android_reason="host_os is android"
	MODULE_LDFLAGS="-module -Xcompiler -nostdlib -Xcompiler -Wl,-rpath -Xcompiler -Wl,/data/data/org.navitproject.navit/lib"
	MODULE_LIBADD="-llog"
	LIBNAVIT=_data_data_org.navitproject.navit_lib_navit
	NAVIT_MODULE_LDFLAGS="$MODULE_LDFLAGS -L\$(top_builddir)/navit -l$LIBNAVIT"
	;;
esac
if test "x$win32" = "xyes"
then
	NAVIT_CFLAGS="$NAVIT_CFLAGS -I\$(top_srcdir)/navit/support/win32"
	NAVIT_LIBS="$NAVIT_LIBS -L\$(top_builddir)/navit/support/win32 -lsupport_win32"
fi


AM_CONDITIONAL(SUPPORT_WIN32, [test "x$win32" = "xyes"])
AM_CONDITIONAL(SUPPORT_WIN32CE, [test "x$win32ce" = "xyes"])
AM_CONDITIONAL(SUPPORT_ANDROID, [test "x$android" = "xyes"])

if test "x$support_libpng" = "xyes"
then
	NAVIT_CFLAGS="$NAVIT_CFLAGS -I\$(top_srcdir)/navit/support/libpng"
	NAVIT_LIBS="$NAVIT_LIBS -L\$(top_builddir)/navit/support/libpng -lsupport_libpng"
fi
AM_CONDITIONAL(SUPPORT_LIBPNG, [test "x$support_libpng" = "xyes"])

AC_SUBST(MODULE_LDFLAGS)
AC_SUBST(MODULE_LIBADD)
AC_SUBST(NAVIT_MODULE_LDFLAGS)

LIBS="$LIBS -lm"
if test "$win32" == "no"; then
	LIBS="$LIBS -rdynamic"
fi

m4_ifndef([AC_USE_SYSTEM_EXTENSIONS],
	[AC_DEFUN([AC_USE_SYSTEM_EXTENSIONS], [AC_GNU_SOURCE])])

AC_USE_SYSTEM_EXTENSIONS

AC_PROG_OBJC
AC_PROG_CC
AC_PROG_OBJC
if eval "test x`uname` = xDarwin"; then
	CFLAGS="$CFLAGS -I/opt/local/include -L/opt/local/lib"
fi
if eval "test x$GCC = xyes"; then
        CFLAGS="$CFLAGS -Wall -Wcast-align -Wmissing-declarations -Wmissing-prototypes -Wstrict-prototypes -Wpointer-arith -Wreturn-type -D_GNU_SOURCE"
fi
AM_PROG_CC_C_O

AC_PROG_CXX
if eval "test x$GXX = xyes"; then
        CXXFLAGS="$CXXFLAGS -Wall -Wcast-align -Wpointer-arith -Wreturn-type -D_GNU_SOURCE"
fi

PKG_CHECK_EXISTS
if test "x${cross_compiling}" = "xyes"; then
	samplemap="no";samplemap_reason="not supported for cross compiling"
	binding_python="no";binding_python_reason="not supported for cross compiling"
	postgresql="no";postgresql_reason="not supported for cross compiling"
	AC_MSG_CHECKING([for a C compiler for build tools])
	AC_CHECK_PROGS(CC_FOR_BUILD, gcc cc)
else
	CC_FOR_BUILD=$CC
fi
CCLD_FOR_BUILD="$CC_FOR_BUILD"
AC_SUBST(cross_compiling)
AC_SUBST(CC_FOR_BUILD)
AC_SUBST(CCLD_FOR_BUILD)

# Endianness
# defines WORDS_BIGENDIAN for big-endian systems
AC_C_BIGENDIAN

AC_ARG_ENABLE(variant, [  --enable-variant=something          set variant], NAVIT_VARIANT=$enableval)
AC_SUBST(NAVIT_VARIANT)

AC_ARG_ENABLE(cache-size, [  --enable-cache-size=size in bytes        set cache size], AC_DEFINE_UNQUOTED(CACHE_SIZE,[${enableval}], [Size of Cache in Bytes]))

AC_ARG_ENABLE(avoid-unaligned, [  --enable-avoid-unaligned          avoid unaligned accesses], AVOID_UNALIGNED=$enableval, AVOID_UNALIGNED=no)
test x"${AVOID_UNALIGNED}" = xyes && AC_DEFINE(AVOID_UNALIGNED,[],Define to avoid unaligned access)

AC_ARG_ENABLE(avoid-float, [  --enable-avoid-float              avoid floating point calculations], AVOID_FLOAT=$enableval, AVOID_FLOAT=no)
test x"${AVOID_FLOAT}" = xyes && AC_DEFINE(AVOID_FLOAT,[],Define to avoid floating point)

AC_ARG_ENABLE(transformation-roll, [  --enable-transformation-roll      add support for specifying roll angle in transformation], ENABLE_ROLL=$enableval, ENABLE_ROLL=no)
test x"${ENABLE_ROLL}" = xyes && AC_DEFINE(ENABLE_ROLL,[],Define to add support for specifying roll angle in transformation)

AC_ARG_ENABLE(hildon, [  --disable-hildon              build without maemo/hildon support], enable_hildon=$enableval, enable_hildon=yes)
if test "x${enable_hildon}" = "xyes" ; then
	PKG_CHECK_MODULES(HILDON, hildon-1 >= 0.9.9, , [
		AC_MSG_RESULT(no)
		enable_hildon=no
	])
	PKG_CHECK_MODULES(GPSBT, gpsbt, [
		AC_DEFINE(HAVE_GPSBT, 1, [Have the gpsbt library])
		AC_SUBST(GPSBT_CFLAGS)
		AC_SUBST(GPSBT_LIBS)
		], [
		AC_MSG_RESULT(no)
	])
	if test x"${enable_hildon}" = xyes ; then
		AC_DEFINE(USE_HILDON, 1, [Build with maemo/hildon support])
		AC_SUBST(HILDON_CFLAGS)
		AC_SUBST(HILDON_LIBS)
	fi
fi
AM_CONDITIONAL(USE_HILDON, test "${enable_hildon}" = "xyes")

AC_ARG_ENABLE(garmin, [  --disable-garmin             disable garmin support], USE_GARMIN=$enableval, USE_GARMIN=yes)

# samplemap
AC_PATH_PROG(_PATH_BZCAT,[bzcat])
if test "x${_PATH_BZCAT}" = "x" ; then
	samplemap=no; samplemap_reason="bzcat missing"
fi
AC_ARG_ENABLE(samplemap, [  --disable-samplemap             don't build the samplemap], samplemap=$enableval;samplemap_reason="configure parameter")
AM_CONDITIONAL(BUILD_SAMPLEMAP, [test "x$samplemap" = "xyes"])

AC_ARG_ENABLE(fastmath, [  --disable-fastmath             don't build with fastmath], fastmath=$enableval, fastmath=yes)
AM_CONDITIONAL(FASTMATH, [test "x$fastmath" = "xyes"])

if test x"$fastmath" = xyes; then
	if eval "test x$GCC = xyes"; then
		CFLAGS="$CFLAGS -ffast-math"
	fi
fi

X_CFLAGS="-I$x_includes"
AS_IF([test -n "$ac_x_libraries"], [X_LIBS="-L$ac_x_libraries"])

# glib
AC_ARG_ENABLE(glib, [  --disable-glib             don't build with external glib], glib=$enableval, glib=yes)
if test x"${glib}" = "xyes"; then
	PKG_CHECK_MODULES(GLIB, [glib-2.0 gthread-2.0], [glib=yes],[glib=no])
fi
if test "x${glib}" = "xyes"; then
	AC_DEFINE(HAVE_GLIB, 1, [Define to 1 if you have (external) glib library])
else
	GLIB_CFLAGS="-I\$(top_srcdir)/navit/support -I\$(top_srcdir)/navit/support/glib -I\$(top_srcdir)/navit/support/ezxml"
	GLIB_LIBS="-L\$(top_builddir)/navit/support/glib -lsupport_glib -L\$(top_builddir)/navit/support/ezxml -lsupport_ezxml"
fi

# gmodule
AC_ARG_ENABLE(gmodule, [  --disable-gmodule             don't build with gmodule], gmodule=$enableval, gmodule=yes)
if test x"${gmodule}" = "xyes"; then
	PKG_CHECK_MODULES(GMODULE, [gmodule-2.0], [gmodule=yes], [gmodule=no])
fi
if test "x${gmodule}" = "xyes"; then
	AC_DEFINE(HAVE_GMODULE, 1, [Define to 1 if you have gmodule])
else
	AC_CHECK_LIB(dl, dlopen,
		[plugins_reason="default, via dlopen";GMODULE_LIBS="-ldl";AC_DEFINE(HAVE_DLOPEN, 1, [Define to 1 if you have dlopen])], 	 
		[plugins="no"; plugins_reason="package gmodule and dlopen missing"]
	)
fi
# plugins
AC_ARG_ENABLE(plugins, 	[  --disable-plugins          disable plugins], [ plugins=$enableval;plugin_reason="configure parameter" ])
if test "x${plugins}" = "xyes"; then
	AC_ENABLE_SHARED
	AC_DISABLE_STATIC
	AC_DEFINE(
		[USE_PLUGINS],
		[],
		Define to 1 if you have plugins.
	)
	if test "x${win32}" = "xyes"; then
		shared_libnavit=yes
		NAVIT_MODULE_LDFLAGS="-no-undefined -L\$(top_builddir)/navit -l$LIBNAVIT -L\$(top_builddir)/intl -lintl"
	fi
else
	AC_DISABLE_SHARED
	AC_ENABLE_STATIC
fi
AM_CONDITIONAL(PLUGINS, [test "x$plugins" = "xyes"])
AM_CONDITIONAL(SHARED_LIBNAVIT, [test "x$shared_libnavit" = "xyes"])
AM_CONDITIONAL(BIN_NAVIT, [test "x$bin_navit" = "xyes"])
AC_PROG_LIBTOOL
AC_SUBST(LIBNAVIT)

AM_CONDITIONAL(EVENT_GLIB, [test "x$glib" = "xyes"])
AM_CONDITIONAL(SUPPORT_GLIB, [test "x$glib" = "xno"])
AM_CONDITIONAL(SUPPORT_EZXML, [test "x$glib" = "xno"])

AC_CHECK_HEADER(
	zlib.h,
	AC_DEFINE(
		[HAVE_ZLIB],
		[],
		Define to 1 if you have the <zlib.h> header file.
	)
	ZLIB_LIBS="-lz"
	zlib=yes,
	ZLIB_CFLAGS="-I\$(top_srcdir)/navit/support/zlib"
	ZLIB_LIBS="-L\$(top_builddir)/navit/support/zlib -lsupport_zlib"
	zlib=no
)
AM_CONDITIONAL(SUPPORT_ZLIB, [test "x$zlib" = "xno"])
AC_SUBST(ZLIB_CFLAGS)
AC_SUBST(ZLIB_LIBS)

# gtk
PKG_CHECK_MODULES(GTK2, [gtk+-2.0], [gtk2_pkgconfig=yes], [gtk2_pkgconfig=no])
if test "x$gtk2_pkgconfig" = "xyes"; then
	AC_DEFINE(HAVE_GTK2, 1, [Define to 1 if you have gtk2])
	graphics_gtk_drawing_area=yes; graphics_gtk_drawing_area_reason="gtk+-2.0 present"
	gui_gtk=yes; gui_gtk_reason="gtk+-2.0 present"
fi

# fsync
AC_MSG_CHECKING(for fsync)
AC_TRY_LINK([#include <unistd.h>], [fsync(0);],AC_MSG_RESULT(yes);AC_DEFINE(HAVE_FSYNC, 1, [Define to 1 if you have the `fsync' function.]),AC_MSG_RESULT(no))

# system
AC_MSG_CHECKING(for system)
AC_TRY_LINK([#include <stdlib.h>], [system("/bin/true");],AC_MSG_RESULT(yes);AC_DEFINE(HAVE_SYSTEM, 1, [Define to 1 if you have the `system' function.]),speech_cmdline=no; speech_cmdline_reason="not supported without system()"; AC_MSG_RESULT(no))

AC_MSG_CHECKING(for CreateProcess)
AC_TRY_LINK([#include <windows.h>], [CreateProcess(NULL,NULL,NULL,NULL,0,0,NULL,NULL,NULL,NULL);],AC_MSG_RESULT(yes);AC_DEFINE(HAVE_CREATEPROCESS, 1, [Define to 1 if you have the `CreateProcess' function.]) speech_cmdline=yes; speech_cmdline_reason="CreateProcess exists", AC_MSG_RESULT(no))

AC_ARG_ENABLE(graphics-sdl, [  --disable-graphics-sdl             don't create graphics sdl], graphics_sdl=$enableval;graphics_sdl_reason="configure parameter")

if test "x${graphics_sdl}" = "xyes" ; then
        PKG_CHECK_MODULES(SDL, 
                          [sdl], 
                          [graphics_sdl="yes"
                           graphics_sdl_reason="sdl present"] , 
                          [graphics_sdl="no"
                           graphics_sdl_reason="sdl not available"]
                         )
        AC_SUBST(SDL_CFLAGS)
        AC_SUBST(SDL_LIBS)
fi
if test "x${graphics_sdl}" = "xyes" ; then
	save_CPPFLAGS=$CPPFLAGS
	CPPFLAGS="$SDL_CFLAGS $CPPFLAGS"
	AC_CHECK_HEADER(SDL_image.h,SDL_IMAGE_LIBS=-lSDL_image,graphics_sdl="no";graphics_sdl_reason="SDL_image.h missing")
	AC_SUBST(SDL_IMAGE_LIBS)
	CPPFLAGS=$save_CPPFLAGS
fi
if test "x${graphics_sdl}" = "xyes" ; then
	AC_DEFINE(USE_GRAPHICS_SDL, 1, [Build with graphics sdl])
fi
AM_CONDITIONAL(USE_GRAPHICS_SDL, test "x${graphics_sdl}" = "xyes")

AC_ARG_ENABLE(postgresql, [  --disable-postgresql             don't add postgresql support to osm2navit], postgresql=$enableval;postgresql_reason="configure parameter")
if test "x${postgresql}" = "xyes" ; then
	if test -z "$PG_CONFIG"; then
            AC_PATH_PROG([PG_CONFIG], [pg_config], [])
        fi
        AC_MSG_CHECKING([for PostgreSQL libraries with $PG_CONFIG])
	if test ! -x "$PG_CONFIG"; then
	    if test "x${PG_CONFIG}" = "x" ; then
	    	 postgresql_reason="$PG_CONFIG not executable"
	    else
	    	 postgresql_reason="pg_config missing"
            fi
	    postgresql=no 
            AC_MSG_RESULT([no])
	else
	    POSTGRESQL_CFLAGS="-I`$PG_CONFIG --includedir`"
	    POSTGRESQL_LIBS="-L`$PG_CONFIG --libdir` -lpq"
    	    AC_DEFINE(HAVE_POSTGRESQL, 1, [Postgresql libraries available])
	    AC_SUBST(POSTGRESQL_CFLAGS)
	    AC_SUBST(POSTGRESQL_LIBS)
            AC_MSG_RESULT([yes])
       	fi 
fi
AM_CONDITIONAL(HAVE_POSTGRESQL, test "x${postgresql}" = "xyes")
# font
# freetype
AC_ARG_ENABLE(font-freetype, [  --disable-font-freetype             don't add freetype support], font_freetype=$enableval;font_freetype_reason="configure parameter")
if test "x${font_freetype}" = "xyes" -a "x${FREETYPE2_CFLAGS}" = "x" -a "x${FREETYPE2_LIBS}" = "x"; then
	PKG_CHECK_MODULES(FREETYPE2, [freetype2], , [font_freetype=no;font_freetype_reason="Package freetype2 missing"])
fi
AC_SUBST(FREETYPE2_CFLAGS)
AC_SUBST(FREETYPE2_LIBS)
AM_CONDITIONAL(FONT_FREETYPE, test "x${font_freetype}" = "xyes")

AC_ARG_ENABLE(fontconfig, [  --disable-fontconfig             don't add fontconfig support], fontconfig=$enableval;fontconfig_reason="configure parameter")
if test "x${fontconfig}" = "xyes"; then
	PKG_CHECK_MODULES(FONTCONFIG, [fontconfig], [fontconfig=yes], [fontconfig=no])
fi
if test "x$fontconfig" = "xyes"; then
   AC_DEFINE(HAVE_FONTCONFIG, 1, [Define to 1 if you have fontconfig])
fi
AC_SUBST(FONTCONFIG_CFLAGS)
AC_SUBST(FONTCONFIG_LIBS)

AM_CONDITIONAL(FONTS, test "x${font_freetype}" = "xyes" -a "x$fontconfig" != "xyes")

PKG_CHECK_MODULES(IMLIB2, [imlib2], [imlib2_pkgconfig=yes], [imlib2_pkgconfig=no])
if test "x$imlib2_pkgconfig" = "xyes"; then
   AC_DEFINE(HAVE_IMLIB2, 1, [Define to 1 if you have imlib2])
fi
AC_SUBST(IMLIB2_CFLAGS)
AC_SUBST(IMLIB2_LIBS)



AC_ARG_ENABLE(gui-sdl, [  --disable-gui-sdl             don't create gui sdl ], MODULE_GUI_SDL=$enableval, MODULE_GUI_SDL=yes)
if test "x$MODULE_GUI_SDL" = "xyes"; then
	AC_CHECK_HEADER(
		SDL/SDL.h,
		AC_DEFINE(
			[HAVE_LIBSDL],
			[],
			Define to 1 if you have the <SDL/SDL.h> header file.
			)
			SDL_LIBS="-lSDL"
			sdl=yes,
		AC_MSG_WARN([*** no SDL/SDL.h -- SDL support disabled])
	)
fi

AC_CHECK_HEADER(
        X11/Xmu/Xmu.h,
        AC_DEFINE(
                [HAVE_XMU],
                [],
                Define to 1 if you have the <X11/Xmu/Xmu.h> header file.
                )
                xmu=yes,
        AC_MSG_WARN([*** no X11/Xmu/Xmu.h -- opengl and SDL support disabled])
)

AC_ARG_ENABLE(graphics-opengl, [  --disable-graphics-opengl      disable graphics type OpenGL], graphics_opengl=$enableval;graphics_opengl_reason="configure parameter")
if test "x${graphics_opengl}" = "xyes" ; then

AC_CHECK_HEADER(
	GL/gl.h,
	AC_DEFINE(
		[HAVE_OPENGL],
		[],
		Define to 1 if you have the <GL/gl.h> header file.
		)
		OPENGL_LIBS="$X_LIBS -lGL -lGLU"
		opengl=yes,
	AC_MSG_WARN([*** no GL/gl.h -- opengl and SDL support disabled];graphics_opengl=no;graphics_opengl_reason="Headers missing")
)

AC_CHECK_HEADER(
	GL/glut.h,
	AC_DEFINE(
		[HAVE_GLUT],
		[],
		Define to 1 if you have the <GL/glut.h> header file.
		)
		glut=yes,
	AC_MSG_WARN([*** no GL/glut.h -- opengl and SDL support disabled]);graphics_opengl=no;graphics_opengl_reason="Headers missing"
)

AC_SUBST(OPENGL_CFLAGS)
AC_SUBST(OPENGL_LIBS)

AC_CHECK_HEADER(
	GL/glc.h,
	AC_DEFINE(
		[HAVE_GLC],
		[],
		Define to 1 if you have the <GL/glc.h> header file.
		)
		GLC_LIBS="-lGLC"
		glc=yes,
	AC_MSG_WARN([*** no GL/glc.h -- opengl and SDL support disabled])
)
AC_SUBST(GLC_CFLAGS)
AC_SUBST(GLC_LIBS)
fi

AM_CONDITIONAL(GUI_SDL, [test "x$sdl" = "xyes" -a "x$opengl" = "xyes" -a "x$glc" = "xyes" -a "x$xmu" = "xyes" ])
AM_CONDITIONAL(GRAPHICS_OPENGL, [test "x$glut" = "xyes" -a "x$opengl" = "xyes" -a "x$glc" = "xyes" ])

system_shapefile=no
AC_CHECK_HEADERS(
		[shapefil.h libshp/shapefil.h],
		AC_DEFINE(
			  [HAVE_SYS_SHAPEFILELIB],
			  [1],
			  Define to 1 if you have the <libshp/shapefil.h> header file.
			  )
		SHAPEFILE_LIBS="-lshp"
		system_shapefile=yes
		)
AC_SUBST(SHAPEFILE_CFLAGS)
AC_SUBST(SHAPEFILE_LIBS)
AM_CONDITIONAL(HAVE_SYSTEM_SHAPEFILELIB, [test "x$system_shapefile" = "xyes" ])
if test x"${system_shapefile}" = xno
then
	AC_MSG_WARN([*** no libshp/shapefil.h -- using included copy])
fi


if test x"${USE_GARMIN}" = xyes
then
	# check for libgarmin
	PKG_CHECK_MODULES(LIBGARMIN, libgarmin, use_libgarmin=yes, use_libgarmin=no)
	AC_SUBST(LIBGARMIN_CFLAGS)
	AC_SUBST(LIBGARMIN_LIBS)
fi
	AM_CONDITIONAL(HAVELIBGARMIN, [test "x$use_libgarmin" = "xyes"])

## binding
# python
AC_ARG_ENABLE(binding-python, [  --disable-binding-python             don't create binding python], binding_python=$enableval;binding_python_reason="configure parameter")
if test "x${binding_python}" = "xyes"; then
	AC_PATH_PROG(_PATH_PYTHON,[python])
	dnl Libraries and flags for embedded Python.
	dnl FIXME: I wish there was a less icky way to get this.
	if test "x${_PATH_PYTHON}" != "x" ; then
		AC_MSG_CHECKING(for Python linkage)
		AC_PATH_PROG([PYTHONCONFIG], [python-config], [])
		if test "x${PYTHONCONFIG}" = "x" ; then
			py_prefix=`$_PATH_PYTHON -c 'import sys; print sys.prefix'`
			py_ver=`$_PATH_PYTHON -c 'import sys; print sys.version[[:3]]'`
			py_lib=`$_PATH_PYTHON -c 'import sys; print sys.lib'`
			py_libdir="${py_prefix}/${py_lib}/python${py_ver}"
			PYTHON_CFLAGS="-I${py_prefix}/include/python${py_ver}"
			if test -f $py_libdir/config/Makefile -a -f $py_prefix/include/python${py_ver}/Python.h; then
				py_libs=`grep '^LIBS=' $py_libdir/config/Makefile | sed -e 's/^.*=//'`
				py_libc=`grep '^LIBC=' $py_libdir/config/Makefile | sed -e 's/^.*=//'`
				py_libm=`grep '^LIBM=' $py_libdir/config/Makefile | sed -e 's/^.*=//'`
				py_liblocalmod=`grep '^LOCALMODLIBS=' $py_libdir/config/Makefile | sed -e 's/^.*=//'`
				py_libbasemod=`grep '^BASEMODLIBS=' $py_libdir/config/Makefile | sed -e 's/^.*=//'`
				PYTHON_LIBS="-L$py_libdir/config $py_libs $py_libc $py_libm -lpython$py_ver $py_liblocalmod $py_libbasemod"
				PYTHON_LIBS=`echo $PYTHON_LIBS | sed -e 's/[ \\t]*/ /g'`
				AC_MSG_RESULT($py_libdir)
			else
				binding_python="no"
				binding_python_reason="$py_libdir/config/Makefile or $py_prefix/include/python${py_ver}/Python.h missing"
			fi

		else
			PYTHON_CFLAGS="`${PYTHONCONFIG} --cflags`"
			PYTHON_LIBS="`${PYTHONCONFIG} --ldflags`"
		fi
	else
		binding_python="no"
		binding_python_reason="python executable missing"
	fi
fi
if test "x${binding_python}" = xyes ; then
	AC_DEFINE(USE_BINDING_PYTHON, 1, [Build with binding python])
fi
AC_SUBST(PYTHON_CFLAGS)
AC_SUBST(PYTHON_LIBS)
AM_CONDITIONAL(BINDING_PYTHON, test "x${binding_python}" = "xyes")

# dbus
AC_ARG_ENABLE(binding-dbus,   [  --disable-binding-dbus               don't create binding dbus], binding_dbus=$enableval;binding_dbus_reason="configure parameter")
if test "x${binding_dbus}" = "xyes" ; then
	PKG_CHECK_MODULES(DBUS, [dbus-glib-1], ,binding_dbus=no)
fi
if test "x${binding_dbus}" = "xyes" ; then
	AC_DEFINE(USE_BINDING_DBUS, 1, [Build with binding dbus])
	vehicle_gpsd_dbus="yes"
	vehicle_gpsd_dbus_reason="dbus binding present"
	speech_dbus="yes"
	speech_dbus_reason="dbus binding present"
fi
AC_SUBST(DBUS_CFLAGS)
AC_SUBST(DBUS_LIBS)
AM_CONDITIONAL(BINDING_DBUS, test "x${binding_dbus}" = "xyes")
AM_CONDITIONAL(VEHICLE_GPSD_DBUS, test "x${vehicle_gpsd_dbus}" = "xyes")
AM_CONDITIONAL(SPEECH_DBUS, test "x${speech_dbus}" = "xyes")
AC_ARG_WITH(dbus-service-dir, [ --with-dbus-service-dir   specify where the dbus service dir resides], DBUS_SERVICE_DIR=$withval, DBUS_SERVICE_DIR="$datarootdir/dbus-1/services")
AC_SUBST(DBUS_SERVICE_DIR)

# svg
AC_ARG_ENABLE(svg, [  --disable-svg        disable Scalable Vector Graphics], enable_svg=$enableval, enable_svg=yes)
AC_ARG_ENABLE(svg2png, [  --disable-svg2png        disable conversion of svgs to pngs], enable_svg2png=$enableval, enable_svg2png=yes)
AC_ARG_ENABLE(svg2png-scaling, [  --enable-svg2png-scaling   enable conversion of svgs to pngs with specified sizes], SVG2PNG_SCALES=$enableval, SVG2PNG_SCALES="8 16 32 48 96")
AC_ARG_ENABLE(svg2png-scaling-flag, [  --enable-svg2png-scaling-flag   enable conversion of flag svgs to pngs with specified sizes], SVG2PNG_SCALES_FLAG=$enableval, SVG2PNG_SCALES_FLAG="8 16 32 48 96")
AC_ARG_ENABLE(svg2png-scaling-nav, [  --enable-svg2png-scaling-nav   enable conversion of nav svgs to pngs with specified sizes], SVG2PNG_SCALES_NAV=$enableval, SVG2PNG_SCALES_NAV="8 16 32 48 96")
AC_ARG_WITH(svg2png-use-convert, [  --with-svg2png-use-convert   use imagemagick's convert for png creation], SVG2PNG_CONVERTER="convert")
AC_ARG_WITH(svg2png-use-rsvg-convert, [  --with-svg2png-use-rsvg-convert   use librsvg's rsvg-convert for png creation], SVG2PNG_CONVERTER="rsvg-convert")
AC_ARG_WITH(svg2png-use-inkscape, [  --with-svg2png-use-inkscape   use inkscapes internal convert routines for png creation], SVG2PNG_CONVERTER="inkscape")
AC_ARG_WITH(svg2png-use-ksvgtopng4, [  --with-svg2png-use-ksvgtopng4   use kde4's ksvgtopng4 for png creation], SVG2PNG_CONVERTER="ksvgtopng4")
AC_ARG_WITH(svg2png-use-ksvgtopng, [  --with-svg2png-use-ksvgtopng   use kde3's convert for png creation], SVG2PNG_CONVERTER="ksvgtopng")
if test "x${enable_svg2png}" = "xyes" ; then
    if test "x${SVG2PNG_CONVERTER}" = "x"; then
        SVG2PNG_CONVERTER="rsvg-convert ksvgtopng ksvgtopng4 inkscape convert"
    fi
    AC_PATH_PROGS([SVG2PNG], ${SVG2PNG_CONVERTER}, [none])
    if test "x${SVG2PNG}" = "xnone"; then
        enable_svg2png="no"
    fi
fi
AC_SUBST(SVG2PNG)
AC_SUBST(SVG2PNG_SCALES)
AC_SUBST(SVG2PNG_SCALES_FLAG)
AC_SUBST(SVG2PNG_SCALES_NAV)
AM_CONDITIONAL(USE_SVG2PNG_SCALES, test "x${SVG2PNG_SCALES}" != "xyes" -a "x${SVG2PNG_SCALES}" != "x")
AM_CONDITIONAL(USE_SVG2PNG_SCALES_FLAG, test "x${SVG2PNG_SCALES_FLAG}" != "xyes" -a "x${SVG2PNG_SCALES_FLAG}" != "x")
AM_CONDITIONAL(USE_SVG2PNG_SCALES_NAV, test "x${SVG2PNG_SCALES_NAV}" != "xyes" -a "x${SVG2PNG_SCALES_NAV}" != "x")
AM_CONDITIONAL(USE_SVG2PNG, test "x${enable_svg2png}" = "xyes")
AM_CONDITIONAL(USE_SVG, test "x${enable_svg}" = "xyes")

# XSLTS
AC_ARG_WITH(xslts, [  --with-xslts   enable processing of navit.xml with given xslt scripts], XSLTS=$withval, XSLTS="")
AC_SUBST(XSLTS)
AC_ARG_WITH(saxon, [ --with-saxon   specify the saxon xslt processor], SAXON=$withval, SAXON="saxon")
AC_SUBST(SAXON)

# Android Permissions
AC_ARG_WITH(android-permissions, [  --with-android-permissions   Set additional android permissions], ANDROID_PERMISSIONS=$withval, ANDROID_PERMISSIONS="")
AC_SUBST(ANDROID_PERMISSIONS)

# NLS

AC_ARG_ENABLE(nls,
  [  --disable-nls        disable Native Language Support ( gettext/libintl )],
   enable_nls=$enableval, enable_nls=yes)


INTLIBS=""
MOFILES=""
POFILES=""
POIFILES=""
LINGUAS=""

if test "x$enable_nls" = "xyes"; then

  AC_CHECK_FUNC(gettext, [HAVEGETTEXT="yes"],[INTLIBS="-lintl" HAVEGETTEXT="yes"])
  AC_CHECK_PROG(XGETTEXT, xgettext, xgettext)
  AC_CHECK_PROG(MSGMERGE, msgmerge, msgmerge)
  AC_CHECK_PROG(MSGFMT, msgfmt, msgfmt)

  if test "$XGETTEXT" != ""; then 
    if $XGETTEXT --help 2>&1 | grep illegal >/dev/null ; then
        echo "xgettext isn't GNU version"
        XGETTEXT=""
    fi
  fi

  if test "$XGETTEXT" != "" ; then
     PO=""
     if test "$LINGUAS" = ""; then
           ling=` (cd $srcdir/po; /bin/ls *.po.in) `
  	   for l in $ling; do
	        lcode=`basename $l .po.in`
	        LINGUAS="$LINGUAS$lcode "
           done
    fi
    AC_DEFINE(ENABLE_NLS, [1], [NLS Please])
    echo "xgettext and gettext() exist; will build i18n support for $LINGUAS"
  else
   LINGUAS=""
   PO=""
   echo "xgettext doesn't exist; will not build i18n support"
   enable_nls=no
 fi
 for lang in $LINGUAS; do
    MOFILES="$MOFILES $lang.mo"
 done
 for lang in $LINGUAS; do
    POFILES="$POFILES $lang.po"
 done
 for lang in $LINGUAS; do
    POIFILES="$POIFILES $lang.po.in"
 done

AC_SUBST(INTLIBS)
AC_SUBST(MOFILES)
AC_SUBST(POFILES)
AC_SUBST(POIFILES)
AM_GNU_GETTEXT_VERSION
AM_GNU_GETTEXT(no-libtool, need-ngettext, \$(top_builddir)/intl/)
AC_SUBST(LIBINTL)
AC_SUBST(LTLIBINTL)
if test x"$LIBINTL" != "x" ;then
	CFLAGS="$CFLAGS -I\$(top_builddir)/intl/"
fi

fi
AM_CONDITIONAL(ENABLE_NLS, [test "x$enable_nls" = "xyes"])
AC_CHECK_HEADER(
	byteswap.h,
	AC_DEFINE(
		[HAVE_BYTESWAP_H],
		[1],
		[Define to 1 if you have byteswap.h],
		)
		,
)

PACKAGE=navit
AC_DEFINE_UNQUOTED(PACKAGE, "$PACKAGE")
AC_DEFINE_UNQUOTED(VERSION, "$VERSION")
AC_SUBST(PACKAGE)
AC_SUBST(VERSION)

AC_CHECK_HEADER(wordexp.h,wordexp_h=yes,wordexp_h=no;NAVIT_CFLAGS="$NAVIT_CFLAGS -I\$(top_srcdir)/navit/support/wordexp";WORDEXP_LIBS="-L\$(top_builddir)/navit/support/wordexp -lsupport_wordexp")
AM_CONDITIONAL(SUPPORT_WORDEXP, [test "x$wordexp_h" = "xno"])

support_libc=no
AC_ARG_ENABLE(support_libc, [  --enable-support-libc enable builtin mini libc ], support_libc=$enableval)
AM_CONDITIONAL(SUPPORT_LIBC, [test "x$support_libc" = "xyes"])
if test "x$support_libc" = "xyes"; then
	CFLAGS="$CFLAGS -I\$(top_srcdir)/navit/support/libc"
	LIBC_LIBS="-L\$(top_builddir)/navit/support/libc -lsupport_libc"
fi

## graphics
AC_ARG_ENABLE(graphics, [  --disable-graphics            disable graphics], graphics=$enableval;graphics_reason="configure parameter")
AM_CONDITIONAL(GRAPHICS, test "x${graphics}" = "xyes")
if test "x$graphics" = "xyes"; then
	AC_DEFINE([USE_GRAPHICS],[1],Define to 1 if you want to use graphics.)
fi	

# gd

# android
AC_ARG_ENABLE(graphics-android, [  --disable-graphics-android            disable graphics type android], graphics_android=$enableval;graphics_android_reason="configure parameter")
AM_CONDITIONAL(GRAPHICS_ANDROID, test "x${graphics_android}" = "xyes")
# gd
AC_ARG_ENABLE(graphics-gd, [  --enable-graphics-gd                enable graphics type gd], graphics_gd=$enableval;graphics_gd_reason="configure parameter")
if test "x${graphics_gd}" = "xyes" ; then
	if test -z "$GDLIB_CONFIG"; then
            AC_PATH_PROG([GDLIB_CONFIG], [gdlib-config], [])
        fi
	AC_MSG_CHECKING([for gdlib with $GDLIB_CONFIG])
	if test ! -x "$GDLIB_CONFIG"; then
	    if test "x${GDLIB_CONFIG}" = "x" ; then
	    	 graphics_gd_reason="$GDLIB_CONFIG not executable"
	    else
	    	 graphics_gd_reason="gdlib-config missing"
            fi
	    graphics_gd=no 
            AC_MSG_RESULT([no])
	else
	    GD_CFLAGS="-I`$GDLIB_CONFIG --includedir`"
	    GD_LIBS="-L`$GDLIB_CONFIG --libdir` -lgd `$GDLIB_CONFIG --libs`"
	    AC_SUBST(GD_CFLAGS)
	    AC_SUBST(GD_LIBS)
            AC_MSG_RESULT([yes])
       	fi 
fi
AM_CONDITIONAL(GRAPHICS_GD, test "x${graphics_gd}" = "xyes")
# gtk_drawing_area
AC_ARG_ENABLE(graphics-gtk-drawing-area, [  --disable-graphics-gtk-drawing-area disable graphics type gtk_drawing_area], graphics_gtk_drawing_area=$enableval;graphics_gtk_drawing_area_reason="configure parameter")
AM_CONDITIONAL(GRAPHICS_GTK_DRAWING_AREA, [test "x${graphics_gtk_drawing_area}" = "xyes"])
# win32
AC_ARG_ENABLE(graphics-win32, [  --disable-graphics-win32            disable graphics type win32], graphics_win32=$enableval;graphics_win32_reason="configure parameter")
AM_CONDITIONAL(GRAPHICS_WIN32, test "x${graphics_win32}" = "xyes")
# qt_qpainter
AC_ARG_ENABLE(graphics-qt-qpainter, [  --disable-graphics-qt-qpainter      disable graphics type qt-qpainter], graphics_qt_qpainter=$enableval;graphics_qt_qpainter_reason="configure parameter")
if test "x${graphics_qt_qpainter}" = "xyes" -a "x${QT_GUI_CFLAGS}" = "x" -a "x${QT_GUI_LIBS}" = "x"; then
	PKG_CHECK_MODULES(QT_GUI, [QtGui QtCore], ,graphics_qt_qpainter=no,graphics_qt_qpainter_reason="Packages QtGui and/or QtCore missing")
	if test "x${graphics_qt_qpainter}" = "xno"; then
		PKG_CHECK_MODULES(QT_GUI, [qt-mt],graphics_qt_qpainter=yes,graphics_qt_qpainter_reason="Package qt-mt present")
	fi
fi
if test "x${graphics_qt_qpainter}" = "xyes" ; then
	AC_DEFINE(USE_GRAPICS_QT_QPAINTER, 1, [Build with graphics qt_qpainter])
	MOC=`$PKG_CONFIG QtGui --variable=moc_location`
	if test "x${MOC}" = "x"; then
		AC_CHECK_PROG(MOC, moc, moc)
	fi
	AC_SUBST(MOC)
fi
AC_SUBST(QT_GUI_CFLAGS)
AC_SUBST(QT_GUI_LIBS)
AM_CONDITIONAL(GRAPHICS_QT_QPAINTER, test "x${graphics_qt_qpainter}" = "xyes")

## gui
# gtk
AC_ARG_ENABLE(gui-gtk, [  --disable-gui-gtk                   disable gui type gtk ], gui_gtk=$enableval)
AM_CONDITIONAL(GUI_GTK, [test "x${gui_gtk}" = "xyes"])
# internal
AC_ARG_ENABLE(gui-internal, [  --disable-gui-internal              disable gui type internal], gui_internal=$enableval;gui_internal_reason="configure parameter")
AM_CONDITIONAL(GUI_INTERNAL, test "x${gui_internal}" = "xyes")
# win32
AC_ARG_ENABLE(gui-win32, [  --disable-gui-win32                 disable gui type win32], gui_win32=$enableval;gui_win32_reason="configure parameter")
AM_CONDITIONAL(GUI_WIN32, test "x${gui_win32}" = "xyes")
# qml
AM_CONDITIONAL(QT_DECLARATIVE_NEWAPI,test "x${gui_qml}" = "xoldapi")
AC_ARG_ENABLE(gui-qml, [  --disable-gui-qml              disable gui type QML], gui_qml=$enableval;gui_qml_reason="configure parameter")
if test "x${gui_qml}" = "xyes" -a "x${QT_GUI_CFLAGS}" = "x" -a "x${QT_GUI_LIBS}" = "x"; then
	PKG_CHECK_MODULES(QT_GUI, [QtGui QtCore], ,gui_qml=no;gui_qml_reason="Packages QtGui and/or QtCore missing")
fi
if test "x${gui_qml}" = "xyes" -a "x${QT_DECLARATIVE_CFLAGS}" = "x" -a "x${QT_DECLARATIVE_LIBS}" = "x"; then
	PKG_CHECK_MODULES(QT_DECLARATIVE, [QtDeclarative >= 4.6.0 ], ,gui_qml=no;gui_qml_reason="Packages QtDeclarative missing")
	QmlApiVersion=`$PKG_CONFIG QtDeclarative --modversion`
	AM_CONDITIONAL(QT_DECLARATIVE_NEWAPI,test "x${QmlApiVersion}" != "x4.6.0" -a "x${QmlApiVersion}" != "x4.6.2" )
fi
if test "x${gui_qml}" = "xyes" ; then
	AC_DEFINE(USE_GUI_QML, 1, [Build with gui qml])
	MOC=`$PKG_CONFIG QtGui --variable=moc_location`
	if test "x${MOC}" = "x"; then
		AC_CHECK_PROG(MOC, moc, moc)
	fi
	AC_SUBST(MOC)
fi
AC_SUBST(QT_GUI_CFLAGS)
AC_SUBST(QT_GUI_LIBS)
AC_SUBST(QT_DECLARATIVE_CFLAGS)
AC_SUBST(QT_DECLARATIVE_LIBS)
AM_CONDITIONAL(GUI_QML, test "x${gui_qml}" = "xyes")

## plugins
# pedestrian
AC_ARG_ENABLE(plugin-pedestrian, [  --enable-plugin-pedestrian          enable pedestrian plugin], plugin_pedestrian=$enableval;plugin_pedestrian_reason="configure parameter")
AM_CONDITIONAL(PLUGIN_PEDESTRIAN, test "x${plugin_pedestrian}" = "xyes")

## routing
AC_ARG_ENABLE(routing, [  --disable-routing            disable routing], routing=$enableval;routing_reason="configure parameter")
AM_CONDITIONAL(ROUTING, test "x${routing}" = "xyes")
if test "x$routing" = "xyes"; then
	AC_DEFINE([USE_ROUTING],[1],Define to 1 if you want to have routing.)
fi

## speech
# android
AC_ARG_ENABLE(speech-android, [  --disable-speech-android            disable speech type android], speech_android=$enableval;speech_android_reason="configure parameter")
AM_CONDITIONAL(SPEECH_ANDROID, test "x${speech_android}" = "xyes")
# cmdline
AC_ARG_ENABLE(speech-cmdline, [  --disable-speech-cmdline            disable speech type cmdline], speech_cmdline=$enableval;speech_cmdline_reason="configure parameter")
AM_CONDITIONAL(SPEECH_CMDLINE, test "x${speech_cmdline}" = "xyes")
# espeak
AC_ARG_ENABLE(speech-espeak, [  --disable-speech-espeak            disable speech type espeak], speech_espeak=$enableval;speech_espeak_reason="configure parameter")
AM_CONDITIONAL(SPEECH_ESPEAK, test "x${speech_espeak}" = "xyes")
# speech-dispatcher
AC_ARG_ENABLE(speech-speech-dispatcher, [  --disable-speech-speech-dispatcher  disable speech type speech-dispatcher], speech_speech_dispatcher=$enableval;speech_speech_dispatcher_reason="configure parameter")
if test "x$speech_speech_dispatcher" = "xyes"; then
	AC_CHECK_HEADER(libspeechd.h, AC_DEFINE([HAVE_LIBSPEECHD],[],Define to 1 if you have the <libspeechd.h> header file.) SPEECHD_LIBS="-lspeechd",  speech_speech_dispatcher=no; speech_speech_dispatcher_reason="libspeechd.h missing")
fi
AC_SUBST(SPEECHD_CFLAGS)
AC_SUBST(SPEECHD_LIBS)
AM_CONDITIONAL(SPEECH_SPEECH_DISPATCHER, test "x${speech_speech_dispatcher}" = "xyes")
AM_CONDITIONAL(SUPPORT_ESPEAK, test "x${support_espeak}" = "xyes")

## vehicle
# android
AC_ARG_ENABLE(vehicle-android, [  --disable-vehicle-android             disable vehicle type android], vehicle_android=$enableval;vehicle_android_reason="configure parameter")
AM_CONDITIONAL(VEHICLE_ANDROID, test "x${vehicle_android}" = "xyes")
# demo
AC_ARG_ENABLE(vehicle-demo, [  --disable-vehicle-demo              disable vehicle type demo], vehicle_demo=$enableval;vehicle_demo_reason="configure parameter")
AM_CONDITIONAL(VEHICLE_DEMO, test "x${vehicle_demo}" = "xyes")
# file
AC_ARG_ENABLE(vehicle-file, [  --disable-vehicle-file              disable vehicle type file], vehicle_file=$enableval;vehicle_file_reason="configure parameter")
AM_CONDITIONAL(VEHICLE_FILE, test "x${vehicle_file}" = "xyes")
# gpsd
AC_ARG_ENABLE(vehicle-gpsd, [  --disable-vehicle-gpsd              disable vehicle type gpsd], vehicle_gpsd=$enableval;vehicle_gpsd_reason="configure parameter")
if test "x${vehicle_gpsd}" = xyes
then
	PKG_CHECK_MODULES([GPSD], [libgps], have_libgps="yes", have_libgps="no")
	if test "x$have_libgps" = "xyes"; then
		AC_DEFINE([HAVE_LIBGPS],[],Define to 1 if you have libgps.)
		PKG_CHECK_MODULES([LIBGPS19], [libgps >= 2.90], have_libgps19="yes", have_libgps19="no")
		if test "x$have_libgps19" = "xyes"; then
			AC_DEFINE([HAVE_LIBGPS19],[],Define to 1 if you have libgps19.)
		fi
	else
		AC_CHECK_HEADER(gps.h, AC_DEFINE([HAVE_LIBGPS],[],Define to 1 if you have the <gps.h> header file.) GPSD_LIBS="-lgps", vehicle_gpsd=no; vehicle_gpsd_reason="no gps.h and no gpsd pkgconfig" )
	fi
fi
AC_SUBST(GPSD_CFLAGS)
AC_SUBST(GPSD_LIBS)
AM_CONDITIONAL(VEHICLE_GPSD, [test "x${vehicle_gpsd}" = "xyes"])
# gypsy
AC_ARG_ENABLE(vehicle-gypsy,[  --disable-vehicle-gypsy             disable vehicle type gypsy], vehicle_gypsy=$enableval;vehicle_gypsy_reason="configure parameter")
if test "x${vehicle_gypsy}" = "xyes"
then
	PKG_CHECK_MODULES(GYPSY, gypsy, ,vehicle_gypsy=no;vehicle_gypsy_reason="package gypsy missing")
fi
AC_SUBST(GYPSY_CFLAGS)
AC_SUBST(GYPSY_LIBS)
AM_CONDITIONAL(VEHICLE_GYPSY, test "x${vehicle_gypsy}" = "xyes")
# maemo
AC_ARG_ENABLE(vehicle-maemo, [  --disable-vehicle-maemo             disable vehicle type maemo], vehicle_maemo=$enableval;vehicle_maemo_reason="configure parameter")
if test "x${vehicle_maemo}" = "xyes" ; then
	PKG_CHECK_MODULES(LIBLOCATION, liblocation, [
		AC_SUBST(LIBLOCATION_CFLAGS)
		AC_SUBST(LIBLOCATION_LIBS)
		], [
		AC_MSG_RESULT(no)
		vehicle_maemo=no
		vehicle_maemo_reason="no maemo location library found"
	])
fi
AM_CONDITIONAL(VEHICLE_MAEMO, test "x${vehicle_maemo}" = "xyes")
# null
AC_ARG_ENABLE(vehicle-null, [  --enable-vehicle-null             enable vehicle type null], vehicle_null=$enableval;vehicle_null_reason="configure parameter")
AM_CONDITIONAL(VEHICLE_NULL, test "x${vehicle_null}" = "xyes")
# wince
AC_ARG_ENABLE(vehicle-wince, [  --disable-vehicle-wince             disable vehicle type wince], vehicle_wince=$enableval;vehicle_wince_reason="configure parameter")
AM_CONDITIONAL(VEHICLE_WINCE, test "x${vehicle_wince}" = "xyes")
# iphone
AC_ARG_ENABLE(vehicle-iphone, [  --disable-vehicle-iphone              disable vehicle type iphone], vehicle_iphone=$enableval;vehicle_iphone_reason="configure parameter")
AM_CONDITIONAL(VEHICLE_IPHONE, test "x${vehicle_iphone}" = "xyes")
if test "x${vehicle_iphone}" = "xyes"
then
	IPHONE_LIBS=-Wl,-framework,CoreLocation
fi
AC_SUBST(IPHONE_CFLAGS)
AC_SUBST(IPHONE_LIBS)
NAVIT_CFLAGS="$NAVIT_CFLAGS $GLIB_CFLAGS $GMODULE_CFLAGS"
NAVIT_LIBS="$NAVIT_LIBS $GLIB_LIBS $GMODULE_LIBS $LIBINTL"
AC_SUBST(NAVIT_CFLAGS)
AC_SUBST(NAVIT_LIBS)
AC_SUBST(WORDEXP_LIBS)
AC_SUBST(LIBC_LIBS)
AC_SUBST(WINDRES)

AC_CONFIG_FILES([
Makefile
navit/Makefile
navit/binding/Makefile
navit/binding/python/Makefile
navit/binding/dbus/Makefile
navit/map/Makefile
navit/map/mg/Makefile
navit/map/textfile/Makefile
navit/map/shapefile/Makefile
navit/map/filter/Makefile
navit/map/binfile/Makefile
navit/map/garmin/Makefile
navit/maptool/Makefile
navit/fib-1.1/Makefile
navit/font/Makefile
navit/font/freetype/Makefile
navit/fonts/Makefile
navit/graphics/Makefile
navit/graphics/android/Makefile
navit/graphics/gd/Makefile
navit/graphics/gtk_drawing_area/Makefile
navit/graphics/opengl/Makefile
navit/graphics/null/Makefile
navit/graphics/sdl/Makefile
navit/graphics/qt_qpainter/Makefile
navit/graphics/win32/Makefile
navit/gui/Makefile
navit/gui/gtk/Makefile
navit/gui/internal/Makefile
navit/gui/win32/Makefile
navit/gui/qml/Makefile
navit/gui/qml/skins/Makefile
navit/gui/qml/skins/navit/Makefile
navit/osd/Makefile
navit/osd/core/Makefile
navit/plugin/Makefile
navit/plugin/pedestrian/Makefile
navit/speech/Makefile
navit/speech/android/Makefile
navit/speech/cmdline/Makefile
navit/speech/dbus/Makefile
navit/speech/espeak/Makefile
navit/speech/speech_dispatcher/Makefile
navit/support/Makefile
navit/support/espeak/Makefile
navit/support/ezxml/Makefile
navit/support/glib/Makefile
navit/support/libc/Makefile
navit/support/libpng/Makefile
navit/support/win32/Makefile
navit/support/wordexp/Makefile
navit/support/zlib/Makefile
navit/vehicle/Makefile
navit/vehicle/android/Makefile
navit/vehicle/file/Makefile
navit/vehicle/gpsd/Makefile
navit/vehicle/gpsd_dbus/Makefile
navit/vehicle/gypsy/Makefile
navit/vehicle/maemo/Makefile
navit/vehicle/null/Makefile
navit/vehicle/demo/Makefile
navit/vehicle/wince/Makefile
navit/vehicle/iphone/Makefile
navit/xpm/Makefile
navit/maps/Makefile
intl/Makefile
po/Makefile
])
#src/data/garmin_img/Makefile

AC_OUTPUT


echo ""
echo ""
echo "Summary of your installation :"
# FIXME : maybe elaborate missing dependencies
if test x"$sdl" != xyes
	then
        sdl_failures="(libsdl maybe?) "
fi
if test x"$xmu" != xyes
        then                
        sdl_failures=$sdl_failures"libxmu "
fi        
if test x"$glut" != xyes
       then
        sdl_failures=$sdl_failures"glut "
fi
if test x"$glc" != xyes
        then
        sdl_failures=$sdl_failures"quesoglc "
fi
if test -z "$sdl_failures"
        then
        echo "OpenGL gui  : ENABLED, with $CEGUI_LIBS"
        else
	         echo "OpenGL gui  : DISABLED : you are missing $sdl_failures"
fi
if test x"$enable_hildon" = xyes
	then
        echo "Maemo/Hildon: ENABLED"
        else
        echo "Maemo/Hildon: DISABLED"
fi
if test x"${USE_GARMIN}" = xyes
	then
	if test "x$use_libgarmin" = "xyes"
		then
		echo "Garmin IMG  : ENABLED"
		else
		echo "Garmin IMG  : DISABLED (you don't have libgarmin)"
	fi
	else 
		echo "Garmin IMG  : DISABLED (you requested it)"
fi

if test x"$LIBINTL" = "x" ;then
	nls_libs="system gettext support"
else
	nls_libs="$LIBINTL"
fi
echo "Plugins:             $plugins ($plugins_reason)"
echo "Postgresql:          $postgresql ($postgresql_reason)"
echo "Samplemap:           $samplemap ($samplemap_reason)"
echo "NLS Support:         $enable_nls ($nls_libs)"
echo "Routing:             $routing ($routing_reason)"
echo "Font renderers:"
echo "  freetype:          $font_freetype ($font_freetype_reason)"
echo "Graphics types: $graphics ($graphics_reason)"
echo "  android:  $graphics_android ($graphics_android_reason)"
echo "  gtk_drawing_area:  $graphics_gtk_drawing_area ($graphics_gtk_drawing_area_reason)"
echo "  qt_qpainter:       $graphics_qt_qpainter ($graphics_qt_qpainter_reason)"
echo "  win32:             $graphics_win32 ($graphics_win32_reason)"
echo "  OpenGL:            $graphics_opengl ($graphics_opengl_reason)"
echo "  gd:                $graphics_gd ($graphics_gd_reason)"
echo "  sdl:               $graphics_sdl ($graphics_sdl_reason)"

echo "GUI types:"
echo "  gtk:               $gui_gtk ($gui_gtk_reason)"
echo "  internal:          $gui_internal ($gui_internal_reason)"
echo "  win32:             $gui_win32 ($gui_win32_reason)"
echo "  qml:               $gui_qml ($gui_qml_reason)"

echo "Bindings:"
echo "  dbus:              $binding_dbus ($binding_dbus_reason)"
echo "  python:            $binding_python ($binding_python_reason)"

echo "Plugins:"
echo "  pedestrian:	   $plugin_pedestrian ($plugin_pedestrian_reason)"

echo "Speech types:"
echo "  android:           $speech_android ($speech_android_reason)"
echo "  cmdline:           $speech_cmdline ($speech_cmdline_reason)"
echo "  dbus:              $speech_dbus ($speech_dbus_reason)"
echo "  espeak:            $speech_espeak ($speech_espeak_reason)"
echo "  speech_dispatcher: $speech_speech_dispatcher ($speech_speech_dispatcher_reason)"

echo "Vehicle types:"
echo "  android:           $vehicle_android ($vehicle_android_reason)"
echo "  demo:              $vehicle_demo ($vehicle_demo_reason)"
echo "  file:              $vehicle_file ($vehicle_file_reason)"
echo "  gpsd:              $vehicle_gpsd ($vehicle_gpsd_reason)"
echo "  gpsd_dbus:         $vehicle_gpsd_dbus ($vehicle_gpsd_dbus_reason)"
echo "  gypsy:             $vehicle_gypsy ($vehicle_gypsy_reason)"
echo "  maemo:             $vehicle_maemo ($vehicle_maemo_reason)"
echo "  null:              $vehicle_null ($vehicle_null_reason)"
echo "  wince:             $vehicle_wince ($vehicle_wince_reason)"
echo "  iphone:            $vehicle_iphone ($vehicle_iphone_reason)"

if  test "x${gtk2_pkgconfig}" != "xyes" -a "x${gui_win32}" != "xyes" -a "x${gui_internal}" != "xyes" -a "x${gui_qml}" != "xyes"
then
	echo ""
	echo "" 
	echo "*** WARNING! you have no gui that can be built! ***"
	echo "Please install the dependency for at least gtk or sdl gui"
	echo "For more details, see the wiki at http://wiki.navit-project.org/"
	echo "" 
	exit 1
fi

